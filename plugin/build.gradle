plugins {
    id 'java'
    id 'de.eldoria.plugin-yml.bukkit' version '0.6.0'
}

version = project.findProperty('projectVersion')

dependencies {
    implementation('org.bstats:bstats-bukkit:3.0.2')
    implementation('dev.dejvokep:boosted-yaml:1.3.5')
    implementation('dev.dejvokep:boosted-yaml-spigot:1.5')
    implementation('me.fixeddev:commandflow-universal:0.6.0')
    implementation('me.fixeddev:commandflow-bukkit:0.6.0')
    implementation('dev.triumphteam:triumph-gui:3.1.11')

    compileOnly('com.sk89q.worldguard:worldguard-bukkit:7.1.0-SNAPSHOT')
    compileOnly('com.sk89q.worldguard:worldguard-core:7.1.0-SNAPSHOT')
    compileOnly("com.github.retrooper:packetevents-spigot:2.7.0")

    compileOnly 'net.citizensnpcs:citizensapi:2.0.35-SNAPSHOT'
    compileOnly "lol.pyr:znpcsplus-api:2.1.0-SNAPSHOT"
    compileOnly("de.oliver:FancyNpcs:2.4.1")

    implementation "net.kyori:adventure-api:4.24.0"
    implementation 'net.kyori:adventure-platform-bukkit:4.4.1'
    implementation "net.kyori:adventure-text-minimessage:4.24.0"
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(21))
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

shadowJar {
    archiveBaseName.set("character-dialogue")

    relocate 'me.fixeddev.commandflow', "me.iatog.characterdialogue.lib.commandflow"
    relocate 'dev.dejvokep.boostedyaml', 'me.iatog.characterdialogue.lib.boostedyaml'
    relocate 'net.kyori', 'me.iatog.characterdialogue.lib.kyori'
    relocate 'org.intellij', 'me.iatog.characterdialogue.lib.intellij'
    relocate 'org.jetbrains', 'me.iatog.characterdialogue.lib.jetbrains'
    relocate 'org.bstats', 'me.iatog.characterdialogue.lib.bstats'
    relocate 'dev.triumphteam.gui', 'me.iatog.characterdialogue.lib.triumphgui'
}

tasks.register('copyJars', Copy) {
    from tasks.named('shadowJar')
    into "$rootDir/build"
}

tasks.named('shadowJar').configure {
    finalizedBy copyJars
}

bukkit {
    name = 'CharacterDialogue'
    main = 'me.iatog.characterdialogue.CharacterDialoguePlugin'
    apiVersion = '1.20'
    version = project.findProperty('projectVersion')
    author = 'iAtog'
    softDepend = [
            'PlaceholderAPI',
            'HolographicDisplays',
            'DecentHolograms',
            'floodgate',
            'ConditionalEvents',
            'Citizens',
            'ZNPCsPlus',
            'FancyNpcs',
            'WorldGuard',
            'packetevents'
    ]
    load = 'POSTWORLD'

    commands {
        characterdialogue {
            description = 'CharacterDialogue main command'
            aliases = ['characterd']
            permission = 'characterdialogue.command'
        }
    }

    permissions {
        'characterdialogue.command.*' {
            description = 'All commands permission'
            children = [
                    'characterdialogue.command.dialogue.*',
                    'characterdialogue.command.gui',
                    'characterdialogue.command.assign',
                    'characterdialogue.command.clear-cache',
                    'characterdialogue.command.item.*',
                    'characterdialogue.command.method',
                    'characterdialogue.command.player-data',
                    'characterdialogue.command.record',
                    'characterdialogue.command'
            ]
        }
        'characterdialogue.command' {
            description = 'Main command permission'
        }

        'characterdialogue.command.clear-cache' {
            description = 'Clears the player\'s cache'
        }
        'characterdialogue.command.assign' {
            description = 'Assign dialogs to an NPC'
        }
        'characterdialogue.command.gui' {
            description = 'Open the CharacterDialogue GUIs'
        }
        'characterdialogue.command.dialogue.*' {
            children = [
                    'characterdialogue.command.dialogue.start',
                    'characterdialogue.command.dialogue.info'
            ]
        }
        'characterdialogue.command.dialogue' {
            children = ['characterdialogue.command.dialogue.info']
            description = 'Dialogue subcommand'
        }
        'characterdialogue.command.dialogue.start' {
            description = 'Start a dialogue with command'
        }
        'characterdialogue.command.dialogue.info' {
            description = 'See dialogue info'
        }
        'characterdialogue.command.group-create' {
            description = 'Create dialogue file with command'
        }

        'characterdialogue.command.item.*' {
            children = [
                    'characterdialogue.command.item.gui',
                    'characterdialogue.command.item.save',
                    'characterdialogue.command.item.delete',
                    'characterdialogue.command.item.give'
            ]
        }
        'characterdialogue.command.item.gui' {
            description = 'See items gui'
        }
        'characterdialogue.command.item.save' {
            description = 'Save main hand item'
        }
        'characterdialogue.command.item.delete' {
            description = 'Delete saved item'
        }
        'characterdialogue.command.item.give' {
            description = 'Give saved item'
        }
        'characterdialogue.command.method' {
            description = 'Method subcommand'
        }
        'characterdialogue.command.player-data' {
            description = 'Manage player data'
        }
        'characterdialogue.command.record' {
            description = 'Recording subcommand'
        }
    }
}